/*                 _                 
 *       /\       (_)            
 *      /  \__   ___ _ __ ___  _ __  
 *     / /\ \ \ / / | '__/ _ \| '_ \ 
 *    / ____ \ V /| | | | (_) | | | |
 *   /_/    \_\_/ |_|_|  \___/|_| |_|
 *
 *
 * Copyright 2025 Aviron
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// -----------------------------------------------------------------------------
// Gradle build
// -----------------------------------------------------------------------------
//
// Aviron uses Gradle 8.9
//
// Build commands
//
//   - Build the Aviron jar
//     $> ./gradlew clean jar
//
// -----------------------------------------------------------------------------
// Release Venice:
//
//   [1] Prepare Aviron
//       - Verify that all unit tests pass: ./gradlew clean test
//       - Change version in 'gradle.properties' to '1.x.y' (remove the
//         SNAPSHOT suffix)
//       - Update the ChangeLog for the release version
//       - Update the 'README.md'
//       - Commit the 'README.md' change
//       - Patch the release version number in:
//           {proj}/doc/maven-central.svg
//           {proj}/README.md    (chapter Getting the latest release)
//
//   [2a] Publish manually to Maven
//       - ./gradlew sonatypeBundle
//       - Open https://central.sonatype.com/publishing and login
//       - Choose deployment name: "com.github.jlangch:aviron:x.y.z"
//       - Upload 'build/sonatype-bundle.zip' (scroll down publish dialog)
//       - Publish the artifact
//
//   [2b] Publish automatically to Maven
//       - ./gradlew -PsonatypeUsername=xxxx -PsonatypePassword=xxxx sonatypePublish
//       - Optionally check at:  https://central.sonatype.com/publishing and login
//
//   [3] Draft a GitHub Venice release
//       - Draft a new release in Git (https://github.com/jlangch/aviron/releases)
//         This will create the release tag
//         => don't miss to set "latest flag"!
//       - Pull the changes (commit tag)
//
//   [4] Open new development cycle
//       - Change version in 'gradle.properties' to '1.x.y-SNAPSHOT'
//       - Add an empty ChangeLog entry for the new version
//       - Commit with message "opened new dev cycle"
//
// -----------------------------------------------------------------------------
// Gradle wrapper
//
//    Upgrade Gradle wrapper:
//      See https://docs.gradle.org/current/userguide/gradle_wrapper.html
//      Upgrade:
//         $> ./gradlew wrapper --gradle-version=8.9 --distribution-type=bin
//
//    Get initial Gradle wrapper
//      See https://guides.gradle.org/creating-new-gradle-builds/
//
//    Create and init a gradle project:
//       $> mkdir test
//       $> cd test
//       $> ./gradlew init
//
// -----------------------------------------------------------------------------

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

def version_regex = '[0-9]+[.][0-9]+[.][0-9]+(-SNAPSHOT)?'

def sonatypeUsername       = project.properties['sonatypeUsername'] ?: "unknown"
def sonatypePassword       = project.properties['sonatypePassword'] ?: "unknown"
def sonatypePublishType    = project.properties['sonatypePublishType'] ?: "USER_MANAGED" // {AUTOMATIC, USER_MANAGED}
def sonatypeBearer         = "${sonatypeUsername}:${sonatypePassword}".bytes.encodeBase64().toString()
def sonatypeDeploymentName = "${project.groupId}:${project.artifactId}:${project.version}"


compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
    options.compilerArgs = [ '-parameters' ]

    doLast {
        println "Java: ${JavaVersion.current()}"
    }
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
    options.compilerArgs += [ '-parameters' ]
}

javadoc.options.encoding = 'UTF-8'


repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {

}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    exclude "com/github/jlangch/aviron/commands/**"
    exclude "com/github/jlangch/aviron/server/**"
    source = sourceSets.main.allJava
}

jar {
    manifest {
        attributes 'Build-Date':             new Date().format("yyyy-MM-dd'T'HH:mm:ss"),
                   'Implementation-Vendor':  'com.github.jlangch',
                   'Implementation-Title':   'Aviron',
                   'Implementation-Version': "${project.version}",
                   'Specification-Vendor':   'com.github.jlangch',
                   'Application-Name':       'Aviron'
    }

    archiveFileName = "aviron-${project.version}.jar"
}

publishing {
    publications {
        MavenCustom(MavenPublication) {
            groupId    = "${project.groupId}"
            artifactId = "${project.artifactId}"
            version    = "${project.version}"

            artifact sourcesJar
            artifact javadocJar
            artifact jar

            pom {
                name = 'Aviron'
                description = 'A Java ClamAV scanner client'
                url = 'https://github.com/jlangch/aviron/'
                inceptionYear = '2025'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jlangch'
                        name = 'Juerg Lang'
                        email = 'juerg.ch@ggaweb.ch'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:jlangch/aviron.git'
                    developerConnection = 'scm:git:git@github.com:jlangch/aviron.git'
                    url = 'http://github.com:jlangch/aviron/tree/master'
                    tag = 'HEAD'
                }
            }
        }
    }

    repositories {
        maven {
            name = "LocalMavenWithChecksums"
            url = uri(layout.buildDirectory.dir("staging-deploy"))
        }
    }
}

signing {
    useGpgCmd()  // use the local GPG agent to sign the artefacts

    sign publishing.publications.MavenCustom
}

tasks.register('sonatypeBundle') {
    // Tech info:         https://central.sonatype.org/publish/publish-portal-upload/
    //                    https://central.sonatype.org/publish/publish-portal-api/
    // Manual publish:    https://central.sonatype.com/publishing
    //                    deployment name "com.github.jlangch:aviron:x.y.z"
    //                    scroll down to upload 'bundle.jar'
    // Browse artifacts:  https://central.sonatype.com/search?namespace=com.github.jlangch
                  
    doLast {
       ant.zip(destfile:"build/sonatype-bundle.zip", basedir:"build/staging-deploy")
       
       println "Bundle:              build/sonatype-bundle.zip"
       println "Publish manually at: https://central.sonatype.com/publishing"
       println "Deployment name:     ${sonatypeDeploymentName}"
    }

    dependsOn clean, jar, sourcesJar, javadocJar, publishMavenCustomPublicationToLocalMavenWithChecksumsRepository
}

task sonatypePublish(type: Exec, dependsOn: [sonatypeBundle]) {
    // Tech info:  https://central.sonatype.org/publish/publish-portal-api/
    //             https://central.sonatype.com/api-doc

    workingDir "$buildDir"

    commandLine "/bin/sh",
                "-c",
                "curl --request POST" +
                "     --no-progress-meter" +
                "     --header 'Authorization: Bearer ${sonatypeBearer}'" +
                "     --form bundle=@sonatype-bundle.zip" +
                "     'https://central.sonatype.com/api/v1/publisher/upload?name=${sonatypeDeploymentName}&publishingType=${sonatypePublishType}'"

    doFirst {
        // prevent publishing snapshot releases
        if(version.toLowerCase().endsWith('snapshot')) {
            throw new GradleException("Rejected publishing snapshot release!")
        }
        println "Publishing to Central Sonatype (mode=${sonatypePublishType})"
    }
}

task updateReleaseVersion {
    doLast {
        if(version.endsWith('SNAPSHOT')) {
            throw new GradleException(
                    "Rejected updating release version. ${project.version} is not a release!")
        }

        println "Updating version ${project.version} -> 'maven-central.svg'"
        ant.replaceregexp(
                match:   "v${version_regex}",
                replace: "v${project.version}",
                flags:   'g'
        ) {
            fileset(dir: 'doc', includes: 'maven-central.svg')
        }

        println "Updating version ${project.version} -> 'README.md'"
        ant.replaceregexp(
                match:   "<version>${version_regex}",
                replace: "<version>${project.version}",
                flags:   'g'
        ) {
            fileset(dir: '.', includes: 'README.md')
        }
    }
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.10.2')
        }
    }
}

java {
    // Apply a specific Java toolchain to ease working on different environments.
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}
